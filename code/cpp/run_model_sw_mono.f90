SUBROUTINE run_model( mesh , dof0 , dof , cost )
   USE m_common
   USE m_linear_algebra
   USE m_mesh
   USE m_time_screen !NOADJ
   USE m_numeric
   USE m_model
   USE m_obs
   implicit none
   !===================================================================================================================!
   ! Interface Variables
   !===================================================================================================================!
   type( msh ), intent(inout) :: mesh
   type( unk ), intent(in ) :: dof0
   type( unk ), intent(inout) :: dof
   real(rp), intent(out) :: cost
   ! Local Variables
   !===================================================================================================================!
   integer(ip) :: sub_nt
   !===================================================================================================================!
   ! Model Loop Time Initialization
   !===================================================================================================================!
   dof%h = dof0%h
   dof%u = dof0%u
   dof%v = dof0%v
   tc = tc0
   nt = nt0
   if ( use_obs == 1 ) then
      innovation (:)%ind_t = 1_ip
   end if
   cost = 0._rp
   !===================================================================================================================!
   ! Writing Initial Condition Output File
   !===================================================================================================================!
   call write_results( dof0 , mesh ) !NOADJ
   !===================================================================================================================!
   ! Initializing post treatment variables
   !===================================================================================================================!
   call sw_pre_treatment( dof0 , mesh ) !NOADJ
   !===================================================================================================================!
   ! SW Model Loop Time
   !===================================================================================================================!
   end_time_loop = .false.
   do while( .not. end_time_loop)
      call sub_run_model
   end do
   !===================================================================================================================!
   ! Cost Function Calculation using Innovation Vector
   !===================================================================================================================!
   call calc_cost_function( cost , mesh )
CONTAINS
   SUBROUTINE sub_run_model
      implicit none
      sub_nt = 0
      do while ( .not. end_time_loop .and. sub_nt < max_nt_for_adjoint )
         !=============================================================================================================!
         ! Boundary Conditions
         !=============================================================================================================!
         call set_bc( dof , mesh )
         !=============================================================================================================!
         ! Time Control
         !=============================================================================================================!
         call advance_time( dof , mesh )
         !=============================================================================================================!
         ! Reinitialization of post variables
         !=============================================================================================================!
         bc%sum_mass_flux(:) = 0._rp
         !=============================================================================================================!
         ! Time Stepping Performing ( Euler + first_b1, IMEX + )
         !=============================================================================================================!
 select case( temp_scheme )
    case( 'euler' )
       select case( spatial_scheme )
      case( 'first_b1' )
       call euler_time_step_first_b1( dof , mesh )
      case default
     end select
                              !<NOADJ
    case( 'imex' )
       select case( spatial_scheme )
      case( 'muscl_b1_b' )
       call imex_time_step( dof , mesh )
      case default
     end select
                              !>NOADJ
    case default
 end select
         !============================================================================!
         ! Post-processing
         !=============================================================================================================!
         call sw_post_treatment( dof , mesh )
         !=============================================================================================================!
         ! Writing Output Result File
         !=============================================================================================================!
         call write_results( dof , mesh ) !NOADJ
         !=============================================================================================================!
         ! Filling Innovation Vector
         !=============================================================================================================!
         if ( use_obs == 1 ) then
            call calc_innovation( dof,mesh )
        endif
         if ( .not. use_obs == 1 ) then
            call update_cost_function( dof , cost )
         endif
         sub_nt = sub_nt + 1
      end do
   END SUBROUTINE sub_run_model
END SUBROUTINE run_model
